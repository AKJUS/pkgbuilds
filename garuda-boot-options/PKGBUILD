# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Librewish <librewish@gmail.com>

pkgname=garuda-boot-options
pkgver="1.0.5"
_commit='e5cd691b48ae784bcd8062c9619b50fe206be541'
pkgrel=1
pkgdesc="A Boot options utility for Garuda Linux"
arch=('any')
url="https://gitlab.com/garuda-linux/applications/garuda-boot-options"
license=('GPL3')
depends=('qt6-base' 'qt6-tools' 'cmake' 'xdg-utils' 'grub' 'polkit')
optdepends=('plymouth')
makedepends=('git')
groups=('garuda')
source=("https://gitlab.com/garuda-linux/applications/garuda-boot-options/-/archive/${_commit}/garuda-boot-options-${_commit}.tar.gz")
sha256sums=('SKIP')
conflicts=('grub-customizer')

build() {
  cmake -B build -S "$pkgname-$_commit" \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
