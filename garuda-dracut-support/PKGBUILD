# Maintainer: TNE <tne [at] garudalinux.org>

pkgname=garuda-dracut-support
pkgdesc="Dracut support for Garuda Linux"
pkgver=1.2.2
pkgrel=1
arch=('any')
license=('GPL')
source=(05-garuda.conf snapshot-overlay.sh module-setup.sh dracut-install-garuda dracut-rebuild 90-dracut-install.hook)
sha256sums=('8b48b3aa1b7599fa5060b0b3c08896d6845da0e1ccd7bf1bfc9b1892c48bf7e2'
            '017588cd975eff20ce60f9d2f498932cc60e7627aefdf9dc21470b2a95da6a32'
            'cdf87309be00c23c2b74dd936be8861b7930edf51f5ea1d3f8f7b1d7dee37924'
            'bce1f961260e282923ecf727cf66ad913bdd87c3b69a8d46f5ed2709d05a7aa5'
            '82b5f2e70530c98e990278a974a58641fa6e699d1b295bb10d2ca2af02c3937b'
            '169f26e3ca8c2af94d8ac0d79e1c6d043b70dcca69f0876c93472ff6035cc12f')

package() {
	depends=('dracut' 'zstd')
	conflicts=('mkinitcpio' 'mkinitcpio-openswap' 'mkinitcpio-systemd-tool' 'garuda-bootctl-dracut' 'dracut-hook')

	cd "$srcdir" || exit
	# Dracut config
	install -Dm644 05-garuda.conf "$pkgdir"/usr/lib/dracut/dracut.conf.d/05-garuda.conf

	# Custom dracut module to create an overlayfs if booted from a snapshot
	install -Dm755 snapshot-overlay.sh "$pkgdir"/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/snapshot-overlay.sh
	install -Dm755 module-setup.sh "$pkgdir"/usr/lib/dracut/modules.d/91btrfs-snapshot-overlay/module-setup.sh

	# Custom version of the dracut hooks from Arch Linux
	install -Dm644 90-dracut-install.hook "${pkgdir}/etc/pacman.d/hooks/90-dracut-install.hook"
	install -Dm755 dracut-install-garuda "${pkgdir}/usr/share/libalpm/scripts/dracut-install-garuda"

	# Rebuild script
	install -Dm755 dracut-rebuild "${pkgdir}/usr/bin/dracut-rebuild"
}
