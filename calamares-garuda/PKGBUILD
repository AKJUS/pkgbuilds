# Maintainer: TNE <TNE[at]garudalinux[dot]org>
# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: librewish <librewish@gmail.com>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares-garuda
pkgver=3.3.10.r47.g16cb360a6
_commit=16cb360a69328a99317cd9a9ebea7213aef23887
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares"
license=('LGPL')
conflicts=('calamares' 'calamares-dev')
provides=('calamares' 'calamares-dev')
replaces=('calamares-dev')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore'
    'boost-libs' 'ckbcomp' 'hwinfo' 'qt6-svg' 'polkit-qt6'
    'squashfs-tools' 'libpwquality' 'python' 'python-distutils-extra')
makedepends=('extra-cmake-modules' 'qt6-tools' 'qt6-translations' 'git' 'boost')

source+=(git+https://github.com/calamares/calamares.git#commit="${_commit}"
    garuda-fswhitelist.patch
    0001-Apply-garuda-specific-patches.patch
    partition.conf
    mount.conf
    mhwdcfg.conf
    chrootcfg.conf)
sha256sums=('894946708bbc9a77d3e0aacd136b63dd8484fbbf78b36f1313e7fa40f50445b8'
            '81dbbfcc96f4e75ff9f11b59b6d715d2da93981810141691af1d58d4068253a7'
            'd681b09c9f6daf22efb08cbcaad577487b4c749205697bf61b96d053ab50db7c'
            '13fefb868e4ad69a1a3cf75fb8e63d075e029b2b9b622b01689571c585bfb244'
            '0a5acb473f36b6627ccf764b29c005f53dee3c1c7ccb2fc9935075027e220f43'
            '13250117481c1ac8d13eac00b2efc0a6b006f19ffe7b93cd87bea04b88169c4b'
            'a98b4ed205c0cc8fa0b7833aec97e48b69aea8f414be4d8f44315ed9f1c62469')

pkgver() {
    cd "$srcdir/calamares" || exit

    _regex='^set\(CALAMARES_VERSION ([0-9]+\.[0-9]+\.[0-9]+([^0-9].*)?)\)\s*$'
    _file='CMakeLists.txt'

    _line=$(
        grep -E "$_regex" "$_file" |
            head -1
    )
    _version=$(
        printf '%s' "$_line" | sed -E "s@$_regex@\1@;s@alpha@a@;s@beta@b@;s@-@.@"
    )
    _line=$(
        printf '%s' "$_line" |
            sed -E 's@\(@\\(@;s@\)@\\)@'
    )
    _commit=$(
        git log -G "$_line" -1 --pretty=oneline --no-color | sed 's@\ .*$@@'
    )
    _revision=$(
        git rev-list --count "$_commit"..HEAD
    )
    _hash=$(
        git rev-parse --short HEAD
    )

    printf '%s.r%s.g%s' \
        "$_version" \
        "$_revision" \
        "$_hash"
}

prepare() {
    cd "${srcdir}/calamares" || exit

    # patches here
    patch -p1 -N -i "$srcdir/garuda-fswhitelist.patch"
    patch -p1 -N -i "$srcdir/0001-Apply-garuda-specific-patches.patch"
}

build() {
    cd "${srcdir}/calamares" || exit

    mkdir -p build && cd build || exit
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DWITH_QT6=ON \
        -DWITH_APPSTREAM=OFF \
        -DBUILD_TESTING=OFF \
        -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg packagechooser packagechooserq \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc"
    make
}

package() {
    cd "${srcdir}" || exit
    install -Dm755 partition.conf "$pkgdir"/etc/calamares/modules/partition.conf
    install -Dm755 mount.conf "$pkgdir"/etc/calamares/modules/mount.conf
    install -Dm755 chrootcfg.conf "$pkgdir"/etc/calamares/modules/chrootcfg.conf

    cd "${srcdir}/calamares/build" || exit
    make DESTDIR="$pkgdir" install
}
