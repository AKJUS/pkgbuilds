# Maintainer: dr460nf1r3 <root at dr460nf1r3 dot org>

pkgname=garuda-rani
pkgver="2.4.0"
_commit='0ff593f8cb66845e6b4ff4a13c804f888331b3be'
pkgrel=1
pkgdesc="Garuda's Reliable Assistant for Native Installations"
arch=('x86_64')
url="https://gitlab.com/garuda-linux/themes-and-settings/settings/garuda-rani"
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'gtk3' 'hicolor-icon-theme' 'libsoup' 'pango' 'webkit2gtk-4.1' 'pacman-contrib' 'garuda-libs' 'garuda-update')
makedepends=('git' 'file' 'openssl' 'appmenu-gtk-module' 'libappindicator-gtk3' 'librsvg' 'base-devel' 'curl' 'wget' 'rustup' 'nodejs-lts-jod' 'dpkg' 'pnpm>10')
optdepends=('paru: show pending AUR updates'
  'meld: compare pacdiff files via a GUI on GTK systems'
  'kompare: compare pacdiff files via a GUI on Qt systems'
  'pace: manage Pacman repositories via a GUI'
  'reflector-simple: update Arch mirrorlists interactively'
  'btrfs-assistant: easily manage Btrfs snapshots and further settings')
conflicts=('garuda-rani-git')
options=('!strip' '!emptydirs' '!lto' '!debug')
source=("https://gitlab.com/garuda-linux/applications/rani/-/archive/${_commit}/rani-${_commit}.tar.gz")
sha256sums=('SKIP')
install=${pkgname}.install

prepare() {
  cd rani-${_commit} || exit
  pnpm install

  rustup default stable
}

build() {
  cd rani-${_commit} || exit
  pnpm run tauri build --no-bundle
}

package() {
  # First build a deb package, as that is inbuilt in Tauri
  cd rani-${_commit} || exit
  pnpm run tauri bundle --bundles deb

  # Then extract the deb package's contents
  cd "$srcdir/rani-${_commit}/src-tauri/target/release/bundle/deb" || exit
  dpkg-deb -x *.deb extracted

  # Now install the extracted files, like binaries, desktop files, icons, etc.
  cd extracted || exit
  install -Dm755 usr/bin/garuda-rani "$pkgdir/usr/bin/garuda-rani"
  install -Dm644 usr/share/applications/org.garudalinux.rani.desktop "$pkgdir/usr/share/applications/org.garudalinux.rani.desktop"
  install -Dm644 usr/share/icons/hicolor/128x128/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/32x32/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/garuda-rani.png"
  install -Dm644 "$srcdir/rani-${_commit}/LICENSE" "$pkgdir/usr/share/licenses/garuda-rani/LICENSE"

  # Copy any assets like translations or application lists
  install -d "$pkgdir/usr/lib/garuda-rani"
  cp -r usr/lib/garuda-rani/* "$pkgdir/usr/lib/garuda-rani"
}
