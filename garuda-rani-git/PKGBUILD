# Maintainer: dr460nf1r3 <root at dr460nf1r3 dot org>

pkgname=garuda-rani-git
pkgver=2.5.0.r13.g3266a59
pkgrel=1
pkgdesc="Garuda's Reliable Assistant for Native Installations"
arch=('any')
url="https://gitlab.com/garuda-linux/applications/rani"
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'gtk3' 'hicolor-icon-theme' 'libsoup' 'pango' 'webkit2gtk-4.1' 'pacman-contrib' 'garuda-libs' 'garuda-update')
makedepends=('git' 'file' 'openssl' 'appmenu-gtk-module' 'libappindicator-gtk3' 'librsvg' 'base-devel' 'curl' 'wget' 'rustup' 'nodejs-lts-jod' 'dpkg' 'pnpm>10')
optdepends=('paru: show pending AUR updates'
  'meld: compare pacdiff files via a GUI on GTK systems'
  'kompare: compare pacdiff files via a GUI on Qt systems'
  'pace: manage Pacman repositories via a GUI'
  'reflector-simple: update Arch mirrorlists interactively'
  'btrfs-assistant: easily manage Btrfs snapshots and further settings')
provides=('garuda-rani')
conflicts=('garuda-rani')
options=('!strip' '!emptydirs' '!lto' '!debug')
source=("git+$url.git")
sha256sums=('SKIP')
install=garuda-rani.install

prepare() {
  cd rani || exit
  pnpm install

  rustup default stable

  # This is a preview version, so logging is verbose by default
  sed -i 's/logLevel = LogLevel.INFO/logLevel = LogLevel.TRACE/g' "$srcdir/rani/src/app/logging/logging.ts"
}

pkgver() {
  cd rani || exit
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd rani || exit
  NX_TUI=false pnpm run tauri build --no-bundle
}

package() {
  # First build a deb package, as that is inbuilt in Tauri
  cd rani || exit
  pnpm run tauri bundle --bundles deb

  # Then extract the deb package's contents
  cd "$srcdir/rani/src-tauri/target/release/bundle/deb" || exit
  dpkg-deb -x *.deb extracted

  # Now install the extracted files, like binaries, desktop files, icons, etc.
  cd extracted || exit
  install -Dm755 usr/bin/garuda-rani "$pkgdir/usr/bin/garuda-rani"
  install -Dm644 usr/share/applications/org.garudalinux.rani.desktop "$pkgdir/usr/share/applications/org.garudalinux.rani.desktop"
  install -Dm644 usr/share/icons/hicolor/128x128/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/32x32/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/garuda-rani.png"
  install -Dm644 "$srcdir/rani/LICENSE" "$pkgdir/usr/share/licenses/garuda-rani/LICENSE"

  # Copy any assets like translations or application lists
  install -d "$pkgdir/usr/lib/garuda-rani"
  cp -r usr/lib/garuda-rani/* "$pkgdir/usr/lib/garuda-rani"
}
