# Maintainer: dr460nf1r3 <root at dr460nf1r3 dot org>

pkgname=garuda-rani-git
pkgver=0.2.0.r0.gd9c2736
pkgrel=1
pkgdesc="Garuda's Reliable Assistant for Native Installations"
arch=('any')
url="https://gitlab.com/garuda-linux/applications/rani"
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'gtk3' 'hicolor-icon-theme' 'libsoup' 'pango' 'webkit2gtk-4.1' 'pacman-contrib' 'garuda-libs')
makedepends=('git' 'file' 'openssl' 'appmenu-gtk-module' 'libappindicator-gtk3' 'librsvg' 'base-devel' 'curl' 'wget' 'rustup' 'nodejs' 'dpkg' 'pnpm>10')
optdepends=('paru: helper for installing games from AUR')
provides=('garuda-rani')
conflicts=('garuda-rani')
options=('!strip' '!emptydirs' '!lto' '!debug')
source=("git+$url.git")
sha256sums=('SKIP')
install=garuda-rani.install

prepare() {
  cd rani
  pnpm install

  rustup default stable
}

pkgver() {
  cd rani
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd rani
  pnpm run tauri build --no-bundle
}

package() {
  cd rani

  pnpm run tauri bundle --bundles deb
  cd "$srcdir/rani/src-tauri/target/release/bundle/deb"
  dpkg-deb -x *.deb extracted

  cd extracted
  install -Dm755 usr/bin/garuda-rani "$pkgdir/usr/bin/garuda-rani"
  install -Dm644 usr/share/applications/org.garudalinux.rani.desktop "$pkgdir/usr/share/applications/org.garudalinux.rani.desktop"
  install -Dm644 usr/share/icons/hicolor/128x128/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/256x256@2/apps/garuda-rani.png"
  install -Dm644 usr/share/icons/hicolor/32x32/apps/garuda-rani.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/garuda-rani.png"
}
