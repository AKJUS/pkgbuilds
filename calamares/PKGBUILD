# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Maintainer: TNE <tne@garudalinux.org>
# Contributor: librewish <librewish@gmail.com>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares
pkgver=3.3.0
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares"
license=('LGPL')
conflicts=('calamares')
provides=('calamares')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore' 'mkinitcpio-openswap'
	'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
	'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'appstream-qt' 'python')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')

source+=("calamares-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz" garuda-fswhitelist.patch 0001-Apply-garuda-specific-patches.patch)
sha256sums=('35fd3f2fed96e444465507bdafee6a982c0f9a6c4c7759e70234d49093743b04'
	'da0c4d0fe1b73b6cf7ad78041123c2b6daae54742f518c1b18350a58efed544c'
	'4c23f656287593d99feaa9f87b5924e5bea4b6682f9eba51ec85603bac8203fa')

prepare() {
	cd "${srcdir}"/calamares-${pkgver} || exit
	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt

	# patches here
	patch -p1 -N -i "$srcdir/garuda-fswhitelist.patch"
	patch -p1 -N -i "$srcdir/0001-Apply-garuda-specific-patches.patch"
	# change version
	#_ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
	_ver="$pkgver"
	printf 'Version: %s-%s' "${_ver}" "${pkgrel}"
	sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}-${pkgrel}|g" CMakeLists.txt
	sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}

build() {
	cd "${srcdir}/calamares-${pkgver}" || exit

	mkdir -p build
	cd build || exit
	cmake .. \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_PYTHONQT:BOOL=ON \
		-DBoost_NO_BOOST_CMAKE=ON \
		-DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc"
	make
}

package() {
	cd "${srcdir}/calamares-${pkgver}/build" || exit
	make DESTDIR="$pkgdir" install

	# rename services-systemd back to services
	mv "$pkgdir/usr/lib/calamares/modules/services-systemd" "$pkgdir/usr/lib/calamares/modules/services"
	mv "$pkgdir/usr/share/calamares/modules/services-systemd.conf" "$pkgdir/usr/share/calamares/modules/services.conf"
	sed -i -e 's/-systemd//' "$pkgdir/usr/lib/calamares/modules/services/module.desc"
	sed -i -e 's/-systemd//' "$pkgdir/usr/share/calamares/settings.conf"
}
