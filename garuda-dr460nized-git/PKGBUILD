# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Co-Maintainer: FGD

pkgname=garuda-dr460nized-git
_pkgname=garuda-dr460nized
pkgver=2.0.12.r49.g1853ac8
pkgrel=5
arch=('any')
url="https://gitlab.com/garuda-linux/themes-and-settings/settings/$_pkgname"
license=('GPL')
source=("git+$url.git#branch=plasma6")
sha256sums=('SKIP')
pkgdesc='Dr460nized settings'
install=$_pkgname.install
provides=('garuda-desktop-settings')
conflicts=('garuda-desktop-settings' 'plasma5-applets-window-appmenu' 'plasma5-applets-window-buttons' 'plasma5-applets-window-title' 'plasma5-applets-betterinlineclock-git' 'plasma5-wallpapers-blurredwallpaper')
replaces=('garuda-dr460nized-next')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
    install -d "$pkgdir"/etc
    cp -rf "$srcdir"/$_pkgname/etc "$pkgdir"
    install -d "$pkgdir"/usr
    cp -rf "$srcdir"/$_pkgname/usr "$pkgdir"
    depends=('beautyline'
        'garuda-common-settings'
        'garuda-icons'
        'garuda-fish-config'
        'garuda-wallpapers'
        'ksysguard'
        'kvantum'
        #'plasma6-applets-window-buttons'
        'plasma6-applets-window-title'
        'plasma5-themes-sweet-full-git'
        'ttf-fira-sans'
        'ttf-firacode-nerd')
}
