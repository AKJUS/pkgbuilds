# Maintainer: FGD
# Co-Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# shellcheck disable=SC2034,2154,2164

pkgname=garuda-mokka
pkgver="1.0.4"
_commit='0b4039a873cfdaaa2f704eaf7510c879b23a2be5'
pkgrel=1
arch=('any')
url="https://gitlab.com/garuda-linux/themes-and-settings/settings/${pkgname}"
license=('GPL')
source=("https://gitlab.com/garuda-linux/themes-and-settings/settings/garuda-mokka/-/archive/${_commit}/garuda-mokka-${_commit}.tar.gz"
  git+https://github.com/catppuccin/kde.git#commit=0909503d388578953142509052b8402a7ef43da5
  git+https://github.com/catppuccin/micro.git#commit=2802b32308e5b1a827689c095f11ae604bbc85e6
  git+https://github.com/catppuccin/conky.git#commit=eff06f767736fbf6d72641d5aef5ea1e507bb760
  git+https://github.com/catppuccin/fish.git#commit=cc8e4d8fffbdaab07b3979131030b234596f18da
  git+https://github.com/catppuccin/konsole#commit=3b64040e3f4ae5afb2347e7be8a38bc3cd8c73a8
  git+https://github.com/catppuccin/bat.git#commit=699f60fc8ec434574ca7451b444b880430319941
  git+https://github.com/catppuccin/grub.git#commit=2a5c8be8185dae49dd22030df45860df8c796312
  git+https://github.com/vinceliuice/Colloid-gtk-theme.git#commit=559faed6062b6c891c9d1f0c778e66841209fb27)
sha256sums=('c14e1dc226cdf33984b978c187a9444517caa0223590a2536cc87b7a88b43a95'
  '57887bc078a1728c15c1d226ebd5902be813d96b0f23f5a346be5bc68d68b320'
  'da7ddf15703372a3432fe3c3fe8265d462ee97fcc0f4b681c61790c2e8d0340f'
  '11810db2a7716bc1d9e93187ddd413e64a7155f5ef99c3b61b408e790e1adae3'
  'eaf82aaafdc83c6569a6805fa5727d18a22dcacc08668a29499c4d263900ec65'
  '9d33648bffaa549068547a755b84fb84a6b0c73d741b0a13195c19a18f5288c4'
  'd83bf03ebacf325d234ff6c7330c279d59580e9eed06f5edc1a292be09d928e5'
  '524c66178ba7d3988f0b3f9f0bedb2f9f0cb720bda8083a3f33b258b25e54b82'
  '00874e98c0f1c82039b6a0e96ff1cf0bda814b303d0f872a8694367cae5e9249')
pkgdesc='Garuda Mokka desktop settings and presets'
makedepends=(git unzip wget sassc)
install=${pkgname}.install
provides=('garuda-desktop-settings')
conflicts=('garuda-desktop-settings'
  'plasma5-themes-sweet-full-git'
  'plasma5-themes-sweet-kde-git')

prepare() {
  mkdir -p ./out ./work

  # Don't call clean_theme function
  cd Colloid-gtk-theme
  sed -i 's/clean_theme && install_theme/install_theme/g' install.sh
}

build() {
  cp -r "${srcdir}/kde" ./work
  pushd ./work/kde &> /dev/null
  bash "${srcdir}/${pkgname}-${_commit}/mokka-install.sh" 1 4 2
  popd &> /dev/null
}

package() {
  cp -r ./work/kde/out/* "${pkgdir}"

  # Install Micro color schemes
  install -d -m 0755 "${pkgdir}/etc/skel/.config/micro/colorschemes"
  cp "${srcdir}/micro/src/catppuccin-mocha.micro" "${pkgdir}/etc/skel/.config/micro/colorschemes/"

  # Install Conky color schemes
  install -d -m 0755 "${pkgdir}"/etc/skel/.config/conky
  cp "${srcdir}/conky/mocha.conf" "${pkgdir}/etc/skel/.config/conky/"

  # Install Fish color schemes
  install -d -m 0755 "${pkgdir}"/etc/skel/.config/fish/themes
  cp "${srcdir}/fish/themes/Catppuccin Mocha.theme" "${pkgdir}/etc/skel/.config/fish/themes/Catppuccin Mocha.theme"

  # Install Konsole color schemes
  install -d -m 0755 "${pkgdir}/usr/share/konsole/themes"
  cp -r "${srcdir}/konsole/themes/catppuccin-mocha.colorscheme" "${pkgdir}/usr/share/konsole/themes/"
  sed -i 's/Opacity=1/Opacity=0.9/g' "${pkgdir}/usr/share/konsole/themes/catppuccin-mocha.colorscheme"

  # Install Bat color schemes
  install -d -m 0755 "${pkgdir}/etc/skel/.config/bat/themes"
  cp -r "${srcdir}/bat/themes/Catppuccin Mocha.tmTheme" "${pkgdir}/etc/skel/.config/bat/themes/"

  # Install GRUB theme
  install -d -m 0755 "${pkgdir}/usr/share/grub/themes/"
  cp -r "${srcdir}/grub/src/catppuccin-mocha-grub-theme" "${pkgdir}/usr/share/grub/themes/catppuccin-mocha"

  cp -r "${srcdir}/${pkgname}-${_commit}/etc" "${pkgdir}/"
  cp -r "${srcdir}/${pkgname}-${_commit}/usr" "${pkgdir}/"

  # Install Mokka-specific wallpapers
  install -d -m 0755 "${pkgdir}/usr/share/wallpapers"
  cp -r "${srcdir}/${pkgname}-${_commit}/wallpapers" "${pkgdir}/usr/share/wallpapers/garuda-mokka"

  # Install Application Menu logo
  install -d -m 0755 "${pkgdir}/usr/share/icons/Tela-circle-dracula/scalable/apps"
  cp "${srcdir}/${pkgname}-${_commit}/usr/share/icons/garuda/distributor-logo-garuda.svg" "${pkgdir}/usr/share/icons/Tela-circle-dracula/scalable/apps/distributor-logo-garuda.svg"

  # Removes standard Catppuccin Mocha Global Theme as Garuda's Mokka theme is used
  rm -rf "${pkgdir}/usr/share/plasma/look-and-feel/Catppuccin-Mocha-Mauve/"

  # GTK theme
  cd Colloid-gtk-theme
  install -d "${pkgdir}/usr/share/themes"
  ./install.sh -n Catppuccin -t purple -c dark --tweaks catppuccin -d "${pkgdir}/usr/share/themes"

  depends=(
    'garuda-common-settings'
    'garuda-fish-config'
    'garuda-icons'
    'garuda-wallpapers'
    'inter-font'
    'ttf-jetbrains-mono-nerd'
    'kvantum'
    'kwin-effects-forceblur'
    'kwin-effect-rounded-corners-git'
    'plasma-applet-window-buttons'
    'plasma-systemmonitor'
    'plasma6-applets-window-title'
    'plasma6-applets-panel-colorizer'
    'plasma6-wallpapers-blurredwallpaper'
    'tela-circle-icon-theme-dracula'
    'xdg-desktop-portal-gtk'
  )
}
